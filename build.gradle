plugins {
	id "java"
	id 'fabric-loom' version '1.3-SNAPSHOT' apply false
}

allprojects {
	apply plugin: "fabric-loom"

	version = "1.0.0"

	repositories {
		// Add repositories to retrieve artifacts from in here.
		// You should only use this when depending on other mods because
		// Loom adds the essential maven repositories to download Minecraft and libraries from automatically.
		// See https://docs.gradle.org/current/userguide/declaring_repositories.html
		// for more information about repositories.

		maven {
			url "https://cursemaven.com"
			content {
				includeGroup "curse.maven"
			}
		}
		mavenCentral()
		maven { url "https://maven.terraformersmc.com/releases/" }
	}

	tasks.withType(JavaCompile).configureEach {
		def targetVersion = 17
		if (JavaVersion.current().isJava9Compatible()) {
			it.options.release = targetVersion
		} else {
			sourceCompatibility = JavaVersion.toVersion(targetVersion)
			targetCompatibility = JavaVersion.toVersion(targetVersion)
		}

		it.options.encoding = "UTF-8"
	}

	dependencies {
		// To change the versions see the gradle.properties file
		minecraft "com.mojang:minecraft:${project.minecraft_version}"
		mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
		modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

		// Fabric API. This is technically optional, but you probably want it anyway.
		modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

		modApi("com.terraformersmc:modmenu:7.1.0") {
			exclude(group: "net.fabricmc.fabric-api")
		}
	}

	jar {
		archiveClassifier.set "dev"
	}

	java {
		withSourcesJar()
	}
}

loom {
	splitEnvironmentSourceSets()

	mods {
		core {
			sourceSet project(':core').sourceSets.main
		}
		example {
			sourceSet project(':example').sourceSets.main
		}
		root {
			sourceSet sourceSets.main
			sourceSet sourceSets.client
		}
	}
}

dependencies {
	afterEvaluate {
		subprojects.each {
			include project("${it.name}:")
		}
	}

	implementation project(path: ":core", configuration: "namedElements")
}

base {
	archivesName = "tutorial-mod"
}
version = "1.0.0"